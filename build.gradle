buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.10.1'
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:1.1.5'
    }
}


group 'test.doc.adoc'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: "org.asciidoctor.convert"
apply plugin: 'com.github.jruby-gradle.base'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    gems 'rubygems:asciidoctor-diagram:1.3.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}


defaultTasks 'asciidoctor'

asciidoctor {
    // jrubyPrepare task is added by the JRuby
    // plugin and will download Ruby gems we have
    // defined in the gems dependency configuration.
    dependsOn jrubyPrepare
    // Asciidoctor task needs the
    // asciidoctor-diagram gem, we installed
    // with the jrubyPrepare task and
    // gems dependency configuration.
    requires = ['asciidoctor-diagram']
    // Set path to find gems to directory
    // used by jrubyPrepareGems task.
    gemPath = jrubyPrepare.outputDir
    // set output direcotry
    outputDir = file("$buildDir/docs")
    // source asciidoc folder
    sourceDir project.file('src/docs/asciidoc')
    // don't create separate folders forgenerated files
    separateOutputDirs = false
    // write log messages aboutdocuments being converted
    logDocuments = true
    // output formats to generate
    backends    'html5','pdf'
    // asciidoctor attributes
    attributes  'build-gradle': file('build.gradle'),
                'source-highlighter': 'coderay',
                'toc': 'right',
                'toc-title' : 'Inhaltsverzeichnis',
                'icons': 'font',
                'idprefix': '',
                'idseparator': '-',
                'setanchors': 'true',
                'docinfo1':'true',
                'imagesdir': file("$buildDir/docs/images"),
                'sourcedir':file("src/main/java")


}